<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">

    <title>$NOFLO_APP_TITLE v$NOFLO_APP_VERSION</title>
    <link rel="shortcut icon" href="$NOFLO_THEME.ico" />
    <link href="https://github.com/noflo/noflo-ui" rel="source" />
    <meta name="description" content="$NOFLO_APP_DESCRIPTION">

    <script src="bower_components/platform/platform.js"></script>
    <link rel="import" href="bower_components/polymer/polymer.html">

    <script src="bower_components/klay-js/klay.js"></script>
    <script src="bower_components/hammerjs/hammer.js"></script>
    <script src="bower_components/react/react.js"></script>
    <script src="browser/noflo-noflo-indexeddb/vendor/IndexedDBShim.min.js"></script>
    
    <link rel="import" href="bower_components/the-graph/the-graph-editor/the-graph-editor.html">
    <noflo-polymer name="the-graph-editor" inports="graph library theme width height" outports="edges nodes"></noflo-polymer>
    <link rel="import" href="browser/noflo-noflo-polymer/noflo-polymer/noflo-polymer.html">
    <link rel="import" href="elements/noflo-context.html">
    <noflo-polymer name="noflo-context" inports="runtimes project graphs component editor search clear getpanel nodes edges runtime" outports="project graphs currentgraph component example newgraph nodes edges result toolpanel contextpanel"></noflo-polymer>
    <link rel="import" href="elements/noflo-main.html">
    <noflo-polymer name="noflo-main" inports="open projects graphs components runtimes route token preparedefaults createdproject" outports="newproject project graph component download githubtoken projects runtimes runtime theme"></noflo-polymer>
    <link rel="import" href="elements/noflo-runtime.html">
    <noflo-polymer name="noflo-runtime" inports="graphs edges panel runtimes runtime" outports="runtime changed"></noflo-polymer>
    <link rel="import" href="elements/noflo-packets.html">
    <noflo-polymer name="noflo-packets" inports="runtime currentgraph edges packet panel" outports=""></noflo-polymer>
    <link rel="import" href="elements/noflo-search.html">
    <noflo-polymer name="noflo-search" inports="toggle results runtimes project graphs component panel" outports="menu search"></noflo-polymer>
    <link rel="import" href="elements/noflo-library.html">
    <noflo-polymer name="noflo-library" inports="editor project graphs search" outports="result"></noflo-polymer>
    <link rel="import" href="elements/noflo-project.html">
    <noflo-polymer name="noflo-project" inports="project runtimes runtime graphs components githubtoken graphs component" outports="newgraph newcomponent changed upload"></noflo-polymer>
    <link rel="import" href="elements/noflo-component-editor.html">
    <noflo-polymer name="noflo-component-editor" inports="component graphs theme width height" outports="changed"></noflo-polymer>
    <link rel="import" href="elements/noflo-journal.html">
    <noflo-polymer name="noflo-journal" inports="db graph editor panel undo redo" outports=""></noflo-polymer>
    <link rel="import" href="elements/noflo-help.html">
    <script src="./browser/noflo-ui.js"></script>

    <link rel="stylesheet" href="./css/font-awesome-shadow.css" shim-shadowdom>
    <link rel="stylesheet" href="./css/noflo-ui.css">
    <link rel="stylesheet" href="./css/noflo-ui-shadow.css" shim-shadowdom>
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no, minimal-ui" />
    <meta name="apple-mobile-web-app-capable" content="yes">
    <meta name="apple-mobile-web-app-status-bar-style" content="black">
    <meta name="format-detection" content="telephone=no">
    <link rel="apple-touch-icon" href="app/$NOFLO_THEME-60.png">
    <link rel="apple-touch-icon" sizes="120x120" href="app/$NOFLO_THEME-120.png">
    <link rel="apple-touch-icon" sizes="144x144" href="app/$NOFLO_THEME-144.png">
    <link rel="apple-touch-icon" sizes="152x152" href="app/$NOFLO_THEME-152.png">
  </head>
  <body class="$NOFLO_THEME loading">
    <!-- $NOFLO_APP_ANALYTICS -->
    <div id="loading">$NOFLO_APP_LOADING</div>
    <a id="bugreport" href="https://github.com/noflo/noflo-ui/issues" title="Report a bug" target="_blank"><i class="fa fa-bug"></i></a>
    <a id="openhelp" href="http://flowhub.io/documentation" title="Help" target="_blank"><i class="fa fa-question"></i></a>
    <noflo-help></noflo-help>
    <main id="container">
      <noflo-main></noflo-main>
      <the-graph-editor></the-graph-editor>
      <noflo-library></noflo-library>
      <noflo-context></noflo-context>
      <noflo-runtime></noflo-runtime>
      <noflo-journal></noflo-journal>
      <noflo-component-editor></noflo-component-editor>
      <noflo-search></noflo-search>
      <noflo-project></noflo-project>
      <noflo-packets></noflo-packets>
    </main>
    <script type="application/fbp" id="PrepareStorage">
      INPORT=Open.NAME:NAME
      OUTPORT=Errors.OUT:ERROR
      OUTPORT=Open.DB:DB

      # Open database connection
      Open(indexeddb/Open) ERROR -> IN Errors(core/Merge)

      # Specify storage version to get correct migrations
      '3' -> VERSION Open

      # Database upgrade setup
      Open UPGRADE -> UPGRADE Upgrade(indexeddb/UpgradeRouter)

      # ## Version 1

      # Project storage
      'projects' -> NAME CreateProjects(indexeddb/CreateStore)
      'id' -> KEYPATH CreateProjects
      Upgrade VERSIONS -> DB CreateProjects
      CreateProjects ERROR -> IN Errors

      # Graph storage
      'graphs' -> NAME CreateGraphs(indexeddb/CreateStore)
      'id' -> KEYPATH CreateGraphs
      CreateProjects DB -> DB CreateGraphs
      CreateGraphs ERROR -> IN Errors
      'project' -> NAME GraphProject(indexeddb/CreateIndex)
      'project' -> KEYPATH GraphProject
      CreateGraphs STORE -> STORE GraphProject
      # Migrate old LocalStorage data
      GraphProject STORE -> GRAPHSTORE Migrate(ui/MigrateLocalStorage)
      GraphProject ERROR -> IN Errors

      # Component storage
      'components' -> NAME CreateComponents(indexeddb/CreateStore)
      'id' -> KEYPATH CreateComponents
      CreateGraphs DB -> DB CreateComponents
      CreateComponents ERROR -> IN Errors
      'project' -> NAME ComponentProject(indexeddb/CreateIndex)
      'project' -> KEYPATH ComponentProject
      CreateComponents STORE -> STORE ComponentProject
      ComponentProject ERROR -> IN Errors

      # ## Version 2

      # Runtime storage
      'runtimes' -> NAME CreateRuntimes(indexeddb/CreateStore)
      'id' -> KEYPATH CreateRuntimes
      Upgrade VERSIONS -> DB CreateRuntimes
      CreateRuntimes ERROR -> IN Errors

      # ## Version 3

      # Journal storage
      'journals' -> NAME CreateJournals(indexeddb/CreateStore)
      'id' -> KEYPATH CreateJournals
      Upgrade VERSIONS -> DB CreateJournals
      CreateJournals ERROR -> IN Errors
      'graph' -> NAME JournalGraph(indexeddb/CreateIndex)
      'graph' -> KEYPATH JournalGraph
      CreateJournals STORE -> STORE JournalGraph
      JournalGraph ERROR -> IN Errors
    </script>
    <script type="application/fbp" id="LoadData">
      INPORT=Read.DB:DB
      INPORT=Start.IN:LOAD
      OUTPORT=GetProjects.ITEM:PROJECTS
      OUTPORT=ConvertGraph.OUT:GRAPHS
      OUTPORT=GetComponents.ITEM:COMPONENTS
      OUTPORT=GetRuntimes.ITEM:RUNTIMES
      OUTPORT=Read.COMPLETE:COMPLETE
      OUTPORT=Errors.OUT:ERROR
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read(indexeddb/BeginTransaction)
      Read ERROR -> IN Errors(core/Merge)
      Start(core/Repeat) OUT -> IN HoldMode
      'projects,graphs,components,runtimes' -> DATA HoldStores(core/Kick) OUT -> STORES Read
      Start OUT -> IN HoldStores

      # Read projects list
      'projects' -> DATA HoldProjectStore(core/Kick) OUT -> NAME ProjectStore(indexeddb/GetStore)
      Start OUT -> IN HoldProjectStore
      Read TRANSACTION -> TRANSACTION ProjectStore
      '' -> DATA HoldAllProjects(core/Kick) OUT -> ALL GetProjects(indexeddb/Query)
      Start OUT -> IN HoldAllProjects
      ProjectStore STORE -> STORE GetProjects

      # Read graphs list
      'graphs' -> DATA HoldGraphStore(core/Kick) OUT -> NAME GraphStore(indexeddb/GetStore)
      Start OUT -> IN HoldGraphStore
      ProjectStore TRANSACTION -> TRANSACTION GraphStore
      '' -> DATA HoldAllGraphs(core/Kick) OUT -> ALL GetGraphs(indexeddb/Query)
      GetProjects ITEM -> IN HoldAllGraphs
      GraphStore STORE -> STORE GetGraphs
      GetGraphs ITEM -> IN ConvertGraph(graph/LoadJson)

      # Read components list
      'components' -> DATA HoldComponentStore(core/Kick) OUT -> NAME ComponentStore(indexeddb/GetStore)
      Start OUT -> IN HoldComponentStore
      GraphStore TRANSACTION -> TRANSACTION ComponentStore
      '' -> DATA HoldAllComponents(core/Kick) OUT -> ALL GetComponents(indexeddb/Query)
      GetProjects ITEM -> IN HoldAllComponents
      ComponentStore STORE -> STORE GetComponents

      # Read runtimes list
      'runtimes' -> DATA HoldRuntimeStore(core/Kick) OUT -> NAME RuntimeStore(indexeddb/GetStore)
      Start OUT -> IN HoldRuntimeStore
      GraphStore TRANSACTION -> TRANSACTION RuntimeStore
      '' -> DATA HoldAllRuntimes(core/Kick) OUT -> ALL GetRuntimes(indexeddb/Query)
      Start OUT -> IN HoldAllRuntimes
      RuntimeStore STORE -> STORE GetRuntimes
    </script>
    <script type="application/fbp" id="StoreGraph">
      INPORT=Convert.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=PutGraph.KEY:KEY
      'toJSON' -> METHOD Convert(objects/CallMethod)
      Convert OUT -> IN EnsureId(local/EnsureId) OUT -> IN Disc(core/DisconnectAfterPacket)
      Disc OUT -> IN SplitGraph(core/Split)
      SplitGraph OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitGraph OUT -> IN HoldMode
      'graphs' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitGraph OUT -> IN HoldStore
      SplitStore OUT -> NAME GraphStore(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION GraphStore STORE -> STORE PutGraph(indexeddb/Put)
      'project' -> KEY GetProject(objects/GetObjectKey)
      SplitGraph OUT -> IN GetProject OBJECT -> VALUE PutGraph
      GetProject OUT -> IN DropProject(core/Drop)
      '' -> DATA HoldProject(core/Kick) OUT -> VALUE SetProject(objects/SetPropertyValue)
      SplitGraph OUT -> IN HoldProject
      'project' -> PROPERTY SetProject
      GetProject MISSED -> IN SetProject OUT -> VALUE PutGraph
    </script>
    <script type="application/fbp" id="PrepareProject">
      INPORT=EnsureId.IN:IN
      INPORT=SplitDb.IN:DB
      OUTPORT=SetMain.OUT:OUT
      OUTPORT=SendGraph.OUT:GRAPH
      'id' -> KEY GetId(objects/GetObjectKey)
      'project' -> PROPERTY SetProject(objects/SetPropertyValue)
      GetId OUT -> VALUE SetProject
      EnsureId(local/EnsureId) OUT -> IN GetId
      'name' -> KEY GetName(objects/GetObjectKey)
      'name' -> PROPERTY SetName(objects/SetPropertyValue)
      GetName OUT -> VALUE SetName
      EnsureId OUT -> IN GetName
      'type' -> KEY GetType(objects/GetObjectKey)
      'type' -> PROPERTY SetType(objects/SetPropertyValue)
      GetType OUT -> VALUE SetType
      EnsureId OUT -> IN GetType
      EnsureId OUT -> START InitializeGraph(objects/CreateObject)
      InitializeGraph OUT -> IN GenGraphId(local/EnsureId) OUT -> IN SetName OUT -> IN SetType OUT -> IN SetProject
      SetProject OUT -> DETAILS ToGraph(graph/CreateGraph)
      SplitDb(core/Split) OUT -> DB StoreGraph(local/StoreGraph)
      ToGraph OUT -> ITEM StoreGraph
      ToGraph OUT -> DATA SendGraph(core/Kick)
      'main' -> PROPERTY SetMain(objects/SetPropertyValue)
      StoreGraph KEY -> VALUE SetMain
      EnsureId OUT -> DATA HoldProject(core/Kick) OUT -> IN SetMain
      SplitDb OUT -> DB StoreProject(local/StoreProject)
      SetMain OUT -> ITEM StoreProject
      StoreGraph KEY -> IN HoldProject
      StoreProject KEY -> IN SendGraph
    </script>
    <script type="application/fbp" id="StoreProject">
      INPORT=Disc.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=PutProject.KEY:KEY
      Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'projects' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE PutProject(indexeddb/Put)
      Split OUT -> VALUE PutProject
    </script>
    <script type="application/fbp" id="StoreComponent">
      INPORT=EnsureId.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=Put.KEY:KEY
      EnsureId(local/EnsureId) OUT -> IN Disc(core/DisconnectAfterPacket)
      Disc OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'components' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
      Split OUT -> VALUE Put
    </script>
    <script type="application/fbp" id="StoreRuntime">
      INPORT=Disc.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=Put.KEY:KEY
      Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'runtimes' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
      Split OUT -> VALUE Put
    </script>
    <script type="application/fbp" id="LoadExample">
      INPORT=SplitGist.IN:GIST
      OUTPORT=SetGraphId.OUT:GRAPH
      OUTPORT=Errors.OUT:ERROR
      'https://api.github.com/gists' -> STRING GistPrefix(strings/SendString)
      '/' -> DELIMITER CreateUrl(strings/CompileString)
      SplitGist(core/Split) OUT -> IN GistPrefix
      GistPrefix OUT -> IN CreateUrl
      SplitGist OUT -> IN CreateUrl
      CreateUrl OUT -> URL GetGist(ajax/GetJsonP)
      'data' -> KEY GetData(objects/GetObjectKey)
      'files' -> KEY GetFiles(objects/GetObjectKey)
      'noflo.json' -> KEY GetFile(objects/GetObjectKey)
      'content' -> KEY GetFileContent(objects/GetObjectKey)
      GetGist OUT -> IN GetData
      GetData OUT -> IN GetFiles OUT -> IN GetFile OUT -> IN GetFileContent
      GetFiles MISSED -> IN Errors
      GetFileContent OUT -> IN ParseExample(strings/ParseJson)
      'id' -> PROPERTY SetGraphId(graph/SetPropertyValue)
      SplitGist OUT -> VALUE SetGraphId
      'project' -> PROPERTY RemoveProject(objects/RemoveProperty)
      ParseExample OUT -> IN ConvertGraph(graph/LoadJson)
      ConvertGraph OUT -> IN RemoveProject OUT -> IN SetGraphId
    </script>
    <script type="application/fbp" id="EnsureId">
      INPORT=Item.IN:IN
      OUTPORT=SetId.OUT:OUT
      'id' -> PROPERTY SetId(objects/SetPropertyValue)
      Item(core/Repeat) OUT -> START Generate(ui/GenerateId)
      Generate OUT -> VALUE SetId
      Item OUT -> IN SetId
    </script>
    <script type="application/fbp" id="GithubUpload">
      INPORT=Repo.IN:REPO
      INPORT=Token.IN:TOKEN
      INPORT=Upload.PATH:NAME
      INPORT=Upload.MESSAGE:MESSAGE
      INPORT=WaitRepo.STRING:CONTENT

      # Plain repository name for repo creation
      '(.*)\/(.*)' -> PATTERN RepoName(strings/Replace)
      '$2' -> REPLACEMENT RepoName
      Repo(core/Repeat) OUT -> IN RepoName(strings/Replace)
      RepoName OUT -> DATA StartCreate(core/Kick)

      # Check if repository exists
      Token(core/Repeat) OUT -> TOKEN CheckRepo
      Repo OUT -> IN CheckRepo(github/GetRepository)
      CheckRepo ERROR -> IN StartCreate
      # If not, we create it
      Token OUT -> TOKEN CreateRepo(github/CreateRepository)
      StartCreate OUT -> IN CreateRepo

      # Once we have repository we can upload the document
      Token OUT -> TOKEN Upload
      Repo OUT -> REPOSITORY Upload(github/SetContents)
      WaitRepo(strings/SendString) OUT -> IN Upload

      # Wait until the repository is ready
      CheckRepo OUT -> IN WaitRepo
      CreateRepo OUT -> IN WaitRepo

      Upload OUT -> IN Drop(core/Drop)
    </script>
    <script type="application/fbp" id="GithubDownload">
      INPORT=Token.IN:TOKEN
      INPORT=Repo.IN:REPO
      INPORT=Project.IN:PROJECT
      OUTPORT=SetComponentLanguage.OUT:COMPONENT
      OUTPORT=SetGraphId.OUT:GRAPH
      OUTPORT=Errors.OUT:ERROR

      # Get project components
      Token(core/Split) OUT -> TOKEN ListComponents(github/GetContents)
      Repo(core/Split) OUT -> REPOSITORY ListComponents
      'false' -> SENDREPO ListComponents
      ListComponents OUT -> IN Drop(core/Drop)
      ListComponents ERROR -> IN Drop
      'components' -> STRING StartComponentList(strings/SendString) OUT -> PATH ListComponents
      Repo OUT -> IN StartComponentList
      'path' -> KEY GetComponentPath(objects/GetObjectKey)
      'false' -> SENDGROUP GetComponentPath
      ListComponents FILES -> IN GetComponentPath
      Token OUT -> TOKEN GetComponent(github/GetContents)
      Repo OUT -> REPOSITORY GetComponent
      'false' -> SENDREPO GetComponent
      GetComponentPath OUT -> PATH GetComponent
      GetComponent OUT -> IN ComponentPath(groups/ReadGroup)
      '(.*)\/(.*)\.(.*)' -> PATTERN CleanComponentName(strings/Replace)
      '$2' -> REPLACEMENT CleanComponentName
      ComponentPath GROUP -> IN CleanComponentName
      '(.*)\.coffee' -> PATTERN DetectCoffee(strings/Replace)
      'coffeescript' -> REPLACEMENT DetectCoffee
      ComponentPath GROUP -> IN DetectCoffee
      '(.*)\.js' -> PATTERN DetectJavaScript(strings/Replace)
      'javascript' -> REPLACEMENT DetectJavaScript
      DetectCoffee OUT -> IN DetectJavaScript
      'project' -> PROPERTY SetComponentProject(objects/SetPropertyValue)
      Project(core/Split) OUT -> VALUE SetComponentProject
      'true' -> KEEP SetComponentProject
      'code' -> PROPERTY SetComponentCode(objects/SetPropertyValue)
      ComponentPath OUT -> VALUE SetComponentCode
      'name' -> PROPERTY SetComponentName(objects/SetPropertyValue)
      CleanComponentName OUT -> VALUE SetComponentName
      'language' -> PROPERTY SetComponentLanguage(objects/SetPropertyValue)
      DetectJavaScript OUT -> VALUE SetComponentLanguage
      ComponentPath GROUP -> START CreateComponent(objects/CreateObject)
      CreateComponent OUT -> IN SetComponentProject OUT -> IN SetComponentCode
      SetComponentCode OUT -> IN SetComponentName OUT -> IN SetComponentLanguage

      # Get project graphs
      Token(core/Split) OUT -> TOKEN ListGraphs(github/GetContents)
      Repo(core/Split) OUT -> REPOSITORY ListGraphs
      'false' -> SENDREPO ListGraphs
      ListGraphs OUT -> IN Drop(core/Drop)
      ListGraphs ERROR -> IN Drop
      'graphs' -> STRING StartGraphList(strings/SendString) OUT -> PATH ListGraphs
      Repo OUT -> IN StartGraphList
      'path' -> KEY GetGraphPath(objects/GetObjectKey)
      'false' -> SENDGROUP GetGraphPath
      ListGraphs FILES -> IN GetGraphPath
      Token OUT -> TOKEN GetGraph(github/GetContents)
      Repo OUT -> REPOSITORY GetGraph
      'false' -> SENDREPO GetGraph
      GetGraphPath OUT -> PATH GetGraph
      GetGraph OUT -> IN GraphPath(groups/ReadGroup)
      '(.*)\/(.*)\.(.*)' -> PATTERN CleanGraphName(strings/Replace)
      '$2' -> REPLACEMENT CleanGraphName
      'project' -> PROPERTY SetGraphProject(graph/SetPropertyValue)
      Project(core/Split) OUT -> VALUE SetGraphProject
      'true' -> KEEP SetGraphProject
      'id' -> PROPERTY SetGraphId(graph/SetPropertyValue)
      CleanGraphName OUT -> VALUE SetGraphId
      'name' -> PROPERTY SetGraphName(objects/SetPropertyValue)
      CleanGraphName OUT -> VALUE SetGraphName
      GraphPath GROUP -> IN CleanGraphName
      GraphPath OUT -> IN LoadGraph(graph/LoadGraph)
      LoadGraph OUT -> IN SetGraphName OUT -> IN SetGraphProject OUT -> IN SetGraphId

      # Error handling
      GetComponent ERROR -> IN Errors(core/Merge)
      GetGraph ERROR -> IN Errors(core/Merge)
      ListComponents ERROR -> IN Errors(core/Merge)
      ListGraphs ERROR -> IN Errors(core/Merge)
    </script>
    <script type="application/fbp" id="Download">
      INPORT=GetToken.IN:IN
      OUTPORT=GetProject.OUT:PROJECT
      OUTPORT=Download.COMPONENT:COMPONENT
      OUTPORT=Download.GRAPH:GRAPH
      OUTPORT=Download.ERROR:ERROR
      'token' -> KEY GetToken(objects/GetObjectKey)
      'project' -> KEY GetProject(objects/GetObjectKey)
      'repo' -> KEY GetRepo(objects/GetObjectKey)
      'id' -> KEY GetProjectId(objects/GetObjectKey)
      GetToken OBJECT -> IN GetProject
      GetProject OUT -> IN GetRepo
      GetRepo OBJECT -> IN GetProjectId

      GetToken OUT -> TOKEN Download(local/GithubDownload)
      GetProjectId OUT -> PROJECT Download
      GetRepo OUT -> REPO Download
    </script>
    <script type="application/fbp" id="Upload">
      INPORT=GetToken.IN:IN
      'token' -> KEY GetToken(objects/GetObjectKey) OUT -> TOKEN Upload(local/GithubUpload)
      'repo' -> KEY GetRepo(objects/GetObjectKey) OUT -> REPO Upload
      'doc' -> KEY GetDoc(objects/GetObjectKey) OUT -> IN MakePath(ui/MakePath) OUT -> NAME Upload
      'message' -> KEY GetMessage(objects/GetObjectKey) OUT -> MESSAGE Upload
      'content' -> KEY GetContent(objects/GetObjectKey) OUT -> CONTENT Upload
      GetToken OBJECT -> IN GetRepo OBJECT -> IN GetDoc OBJECT -> IN GetMessage OBJECT -> IN GetContent
    </script>
    <script type="application/fbp" id="ProjectContext">
      INPORT=Context.PROJECT:PROJECT
      INPORT=Context.GRAPHS:GRAPHS
      INPORT=Context.COMPONENT:COMPONENT
      INPORT=Runtimes.IN:RUNTIMES
      INPORT=Context.CLEAR:CLEAR
      INPORT=Project.GITHUBTOKEN:GITHUBTOKEN
      INPORT=Journal.DB:DB
      INPORT=Theme.IN:THEME
      OUTPORT=ListenGraph.OUT:GRAPHCHANGED
      OUTPORT=ComponentEditor.CHANGED:COMPONENTCHANGED
      OUTPORT=MergeNewGraph.OUT:NEWGRAPH
      OUTPORT=Project.NEWCOMPONENT:NEWCOMPONENT
      OUTPORT=Project.CHANGED:PROJECTCHANGED
      OUTPORT=RuntimeChanged.OUT:RUNTIMECHANGED
      OUTPORT=Runtime.RUNTIME:RUNTIME
      OUTPORT=Project.UPLOAD:UPLOAD
      'noflo-context' -> SELECTOR Context(polymer/noflo-context)

      # Runtime handling
      Runtimes(core/Split) OUT -> RUNTIMES Runtime
      Runtimes OUT -> RUNTIMES Context
      Context GRAPHS -> GRAPHS Runtime(local/HandleRuntime)
      Context EDGES -> EDGES Runtime
      Runtime CHANGED -> IN RuntimeChanged(core/Merge)
      Runtime RUNTIME -> RUNTIME Context

      # Search handling
      'noflo-search' -> SELECTOR Search(polymer/noflo-search)
      Runtimes OUT -> RUNTIMES Search
      Context GRAPHS -> GRAPHS Search
      Context COMPONENT -> COMPONENT Search
      Context PROJECT -> PROJECT Search

      's' -> KEYS Shortcuts(interaction/ListenKeyboardShortcuts)
      Shortcuts SHORTCUT -> TOGGLE Search

      # Journal history handling
      'noflo-journal' -> SELECTOR Journal(polymer/noflo-journal)
      Context CURRENTGRAPH -> GRAPH Journal
      #'z' -> KEYS Shortcuts
      #Shortcuts SHORTCUT -> UNDO Journal
      #'r' -> KEYS Shortcuts
      #Shortcuts SHORTCUT -> REDO Journal

      # Panel handling
      'noflo-packets' -> SELECTOR Packets(polymer/noflo-packets)
      '' -> GETPANEL Context TOOLPANEL -> PANEL Runtime
      Context CONTEXTPANEL -> PANEL Search
      Context CONTEXTPANEL -> PANEL Packets

      # Library handling
      'noflo-library' -> SELECTOR Library(polymer/noflo-library)
      Context PROJECT -> PROJECT Library
      Context GRAPHS -> GRAPHS Library

      # UI searching
      Search SEARCH -> SEARCH Library RESULT -> RESULTS Search
      Search SEARCH -> SEARCH Context RESULT -> RESULTS Search

      # Project menu
      'noflo-project' -> SELECTOR Project(polymer/noflo-project)
      Runtimes OUT -> RUNTIMES Project
      Runtime RUNTIME -> RUNTIME Project
      Context PROJECT -> PROJECT Project
      Context GRAPHS -> GRAPHS Project
      Context COMPONENT -> COMPONENT Project
      Project NEWGRAPH -> IN MergeNewGraph(core/Merge)
      Context NEWGRAPH -> IN MergeNewGraph

      # Prepare graph editor
      Context CURRENTGRAPH -> CURRENTGRAPH GraphEditor(local/GraphEditor)
      Theme(core/Repeat) OUT -> THEME GraphEditor
      Runtime RUNTIME -> RUNTIME GraphEditor
      Context NEWGRAPH -> NEWGRAPH GraphEditor
      Context EXAMPLE -> EXAMPLE GraphEditor
      Context PROJECT -> PROJECT GraphEditor
      GraphEditor EDITOR -> EDITOR Library
      GraphEditor EDITOR -> EDITOR Context
      GraphEditor EDGES -> EDGES Context
      GraphEditor NODES -> NODES Context
      GraphEditor EDITOR -> EDITOR Journal
      GraphEditor RUNTIMECHANGED -> IN RuntimeChanged


      # Prepare component editor
      'noflo-component-editor' -> SELECTOR ComponentEditor(polymer/noflo-component-editor)
      Context COMPONENT -> COMPONENT ComponentEditor
      Theme OUT -> THEME ComponentEditor

      # Listen for graph changes
      Context CURRENTGRAPH -> IN ListenGraph(graph/ListenChanges)
      Runtime RUNTIME -> RUNTIME SendGraphChanges(runtime/SendGraphChanges)
      Context CURRENTGRAPH -> GRAPH SendGraphChanges

      # Listen for network packets
      Context EDGES -> EDGES Packets
      Runtime RUNTIME -> RUNTIME Packets
      GraphEditor PACKET -> PACKET Packets
      Context CURRENTGRAPH -> CURRENTGRAPH Packets

      # Listen for screen size changes and communicate with editors
      '' -> START ListenWindow(interaction/ListenResize)
      ListenWindow WIDTH -> WIDTH GraphEditor
      ListenWindow HEIGHT -> HEIGHT GraphEditor
      ListenWindow WIDTH -> WIDTH ComponentEditor
      ListenWindow HEIGHT -> HEIGHT ComponentEditor
    </script>
    <script type="application/fbp" id="GraphEditor">
      INPORT=ConnectRuntime.RUNTIME:RUNTIME
      INPORT=ConnectRuntime.PROJECT:PROJECT
      INPORT=ConnectRuntime.NEWGRAPH:NEWGRAPH
      INPORT=ConnectRuntime.EXAMPLE:EXAMPLE
      INPORT=Graph.IN:CURRENTGRAPH
      INPORT=GraphEditor.WIDTH:WIDTH
      INPORT=GraphEditor.HEIGHT:HEIGHT
      INPORT=GraphEditor.THEME:THEME
      OUTPORT=Async.OUT:EDITOR
      OUTPORT=ConnectRuntime.PACKET:PACKET
      OUTPORT=GraphEditor.EDGES:EDGES
      OUTPORT=GraphEditor.NODES:NODES
      OUTPORT=ConnectRuntime.RUNTIME:RUNTIMECHANGED
      'the-graph-editor' -> SELECTOR GraphEditor(polymer/the-graph-editor)
      Graph(core/Repeat) OUT -> START EmptyLibrary(objects/CreateObject)
      EmptyLibrary OUT -> LIBRARY GraphEditor
      Graph(core/Repeat) OUT -> GRAPH GraphEditor
      GraphEditor ELEMENT -> IN Async(core/RepeatAsync)
      Async OUT -> EDITOR ConnectRuntime(ui/ConnectRuntime)
    </script>
    <script type="application/fbp" id="HandleRuntime">
      INPORT=RuntimeElement.GRAPHS:GRAPHS
      INPORT=RuntimeElement.EDGES:EDGES
      INPORT=RuntimeElement.PANEL:PANEL
      INPORT=RuntimeElement.RUNTIMES:RUNTIMES
      OUTPORT=Connect.RUNTIME:RUNTIME
      OUTPORT=RuntimeElement.CHANGED:CHANGED

      'noflo-runtime' -> SELECTOR RuntimeElement(polymer/noflo-runtime)
      'body' -> SELECTOR GetParent(dom/GetElement) ELEMENT -> ELEMENT Connect(runtime/ConnectRuntime)
      RuntimeElement RUNTIME -> DEFINITION Connect
      Connect RUNTIME -> RUNTIME RuntimeElement
    </script>
    <script type="application/fbp" id="LoadRouteData">
      INPORT=GetNode.ROUTE:ROUTE
      INPORT=GetNode.PROJECTS:PROJECTS
      INPORT=GetSource.RUNTIME:RUNTIME
      OUTPORT=GetNode.PROJECT:PROJECT
      OUTPORT=MergeGraphs.OUT:GRAPH
      OUTPORT=MergeComponents.OUT:COMPONENT

      # Locally available graphs and components
      GetNode(ui/GetNode) GRAPH -> IN MergeGraphs(core/Merge)
      GetNode COMPONENT -> IN MergeComponents(core/Merge)

      # Example handling
      GetNode EXAMPLE -> GIST LoadExample(local/LoadExample)
      LoadExample GRAPH -> IN MergeGraphs

      # Runtime component handling
      GetNode RUNTIME -> NAME GetSource(runtime/GetSource)
      'true' -> VALUE SetReadOnly(objects/SetPropertyValue)
      'readonly' -> PROPERTY SetReadOnly
      'true' -> KEEP SetReadOnly
      GetSource SOURCE -> IN SetReadOnly OUT -> IN MergeComponents
      GetSource ERROR -> IN Drop(core/Drop)
    </script>
    <script type="application/fbp" id="main">
      # Name of our IndexedDB database
      'noflo-ui' -> NAME Storage(local/PrepareStorage)

      # Prepare the main view
      'noflo-main' -> SELECTOR Main(polymer/noflo-main)
      '' -> PREPAREDEFAULTS Main

      # Pass database connection to persistence handlers
      Storage DB -> DB LoadData(local/LoadData)
      Storage DB -> DB StoreProject(local/StoreProject)
      Storage DB -> DB PrepareProject(local/PrepareProject)
      Storage DB -> DB StoreComponent(local/StoreComponent)
      Storage DB -> DB StoreGraph(local/StoreGraph)
      Storage DB -> DB StoreRuntime(local/StoreRuntime)
      Storage DB -> IN DatabaseReady(ui/DatabaseReady)
      Storage ERROR -> IN ShowErrors(core/Output)

      # Start by loading user's local data
      '' -> LOAD LoadData
      LoadData ERROR -> IN ShowErrors
      LoadData PROJECTS -> PROJECTS Main
      LoadData GRAPHS -> GRAPHS Main
      LoadData COMPONENTS -> COMPONENTS Main
      LoadData RUNTIMES -> RUNTIMES Main

      # Handle data changes from main
      Main NEWPROJECT -> IN PrepareProject OUT -> PROJECTS Main
      PrepareProject GRAPH -> GRAPHS Main
      PrepareProject OUT -> DATA HoldNewProject(core/Kick) OUT -> CREATEDPROJECT Main
      PrepareProject GRAPH -> IN HoldNewProject
      Main RUNTIME -> ITEM StoreRuntime

      # Prepare the context
      Router(ui/Router) ROUTE -> CLEAR Context(local/ProjectContext)
      Storage DB -> DB Context
      Main RUNTIMES -> RUNTIMES Context
      Router ROUTE -> ROUTE RouteData(local/LoadRouteData)
      Main PROJECTS -> PROJECTS RouteData
      RouteData PROJECT -> PROJECT Context
      RouteData GRAPH -> GRAPHS Context
      RouteData COMPONENT -> COMPONENT Context
      Main THEME -> THEME Context
      Context COMPONENTCHANGED -> ITEM StoreComponent
      Context COMPONENTCHANGED -> COMPONENTS Main
      Context GRAPHCHANGED -> IN Check(ui/IgnoreExamples) OUT -> ITEM StoreGraph
      Context PROJECTCHANGED -> ITEM StoreProject
      Context RUNTIME -> RUNTIME RouteData
      Context RUNTIMECHANGED -> ITEM StoreRuntime
      Context NEWGRAPH -> IN Check(ui/IgnoreExamples) OUT -> ITEM StoreGraph
      Context NEWCOMPONENT -> ITEM StoreComponent

      # Handle GitHub tokens
      '' -> TOKEN Main GITHUBTOKEN -> GITHUBTOKEN Context
      Main DOWNLOAD -> IN Download(local/Download)
      Download PROJECT -> ITEM StoreProject
      Download PROJECT -> PROJECTS Main
      Download GRAPH -> ITEM StoreGraph
      Download GRAPH -> GRAPHS Main
      Download COMPONENT -> ITEM StoreComponent
      Download COMPONENT -> COMPONENTS Main
      Download ERROR -> IN ShowErrors
      Context UPLOAD -> IN Upload(local/Upload)

      # Show/hide main widget
      'true' -> DATA ShowMain(core/Kick) OUT -> OPEN Main
      'false' -> DATA HideMain(core/Kick) OUT -> OPEN Main
      Router MAIN -> IN ShowMain
      Router ROUTE -> IN HideMain

      # Routing
      LoadData COMPLETE -> START ListenHash(interaction/ListenHash)
      ListenHash INITIAL -> URL Router
      ListenHash CHANGE -> URL Router
    </script>
    <script src="app/noflo.js"></script>
  </body>
</html>
